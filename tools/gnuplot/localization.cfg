# helper functions for drawing of arrows from localization results
#
# Copyright (c) Hagen Wierstorf
#
# You can redistribute the material and/or modify it under the terms of
# Creative Commons Attribution 3.0 (CC BY 3.0)
#
# Link to License: https://creativecommons.org/licenses/by/3.0/
#
# Modifications done by Fiete Winter

# load mathematical helper functions like deg(), rad()
load 'mathematics.cfg'

# @localization_arrow
# arrow drawn at the point the listener is sitting, pointing toward the
# perceived direction
set palette defined ( 0 '#008000', \
1 '#ffff00',\
2 '#ff0000')
arrow_length = 0.08
arrow_x(phi) = 0.5*arrow_length*cos(rad(phi))
arrow_y(phi) = 0.5*arrow_length*sin(rad(phi))
localization_arrow = 'u ($2-arrow_y($3)):($1-arrow_x($3)):(2*arrow_y($3)):(2*arrow_x($3)):(abs($4)) with vectors head size 0.05,20,60 filled lc palette lw 1 lt 1'

# @localization_grey_line
# line going from the listener position towards the edge of the figure
set style line 202 lc rgb '#d6d7d9' pt 7 ps 2 lt 1 lw 1
grey_line_size = 500
grey_line_x(phi) = (grey_line_size+0.1)/50.0*cos(rad(phi)+pi/2)
grey_line_y(phi) = (grey_line_size+0.1)/50.0*sin(rad(phi)+pi/2)
localization_grey_line = 'u 1:2:(grey_line_x($3)):(grey_line_y($3)) with vectors nohead ls 202'

# @point_source
# plot a point source (large grey point) at the specific point
set_point_source(x,y) = sprintf('<echo "%f %f"',x,y)
point_source = 'w p lc rgb "gray" pt 7 ps 1.7'

# @plane_wave
# plot three lines with an arrow to symbolize a plane wave
set_plane_wave(x,y,s,phi) = sprintf('<echo "\
%f %f\n\
%f %f\n\
\n\
%f %f\n\
%f %f\n\
\n\
%f %f\n\
%f %f\n\
\n\
%f %f\n\
%f %f\n\
\n\
%f %f\n\
%f %f\n\
\n\
%f %f\n\
%f %f"',\
x+rotx(-s,0,phi),y+roty(-s,0,phi),\
x+rotx( s,0,phi) ,y+roty(s,0,phi) ,\
x+rotx(-s,s*0.1,phi),y+roty(-s,s*0.1,phi),\
x+rotx( s,s*0.1,phi),y+roty( s,s*0.1,phi),\
x+rotx(-s,-s*0.1,phi),y+roty(-s,-s*0.1,phi),\
x+rotx( s,-s*0.1,phi),y+roty( s,-s*0.1,phi),\
x+rotx( 0,-s*0.1,phi),y+roty( 0,-s*0.1,phi),\
x+rotx( 0,-s*0.4,phi),y+roty( 0,-s*0.4,phi),\
x+rotx( 0,-s*0.4,phi),y+roty( 0,-s*0.4,phi),\
x+rotx( s*0.1,-s*0.3,phi),y+roty( s*0.1,-s*0.3,phi),\
x+rotx( 0,-s*0.4,phi),y+roty( 0,-s*0.4,phi),\
x+rotx( -s*0.1,-s*0.3,phi),y+roty( -s*0.1,-s*0.3,phi)\
)
plane_wave = 'w l lc rgb "#404040" lt 1 lw 1'

# @focused_source
# plot a focused source (large grey open point) at the specific position
set_focused_source(x,y) = sprintf('<echo "%f %f"',x,y)
focused_source = 'w p lc rgb "#404040" pt 7 ps 1.7 lw 3'
